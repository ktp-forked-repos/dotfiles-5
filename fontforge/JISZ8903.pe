#!/usr/bin/fontforge -script

Open("JISZ8903-Medium.otf")
Reencode("unicode")

# ---- Rescaling

Print("JISZ8903: Rescaling some glyphs ...")

# make numbers and capital letters smaller
Select(0u0030, 0u0039, 0u0041, 0u005A) # 0-9, A-Z
Scale(80, 100, 0, 0)

# make other ascii slightly smaller
Select(0u0020, 0u007E)          # ASCII
SelectFewer(0u0030, 0u0039, 0u0041, 0u005A)
Scale(95, 100, 0, 0)

# make wide characters more smaller
SelectSingletons(0u004D, 0u0057, 0u006D, 0u0077) # M, W, m, w
Scale(80, 100, 0, 0)

# ---- Monospacify

Print("JISZ8903: Monospacifying ...")

# monospacify (halfwidth: 650)
Select(0u0020, 0u007E)          # ASCII
SetWidth(650)

# monospacify (halfwidth: 1300)
SelectWorthOutputting()
SelectFewer(0u0020, 0u007E)
SetWidth(1300)

# ---- Positioning

Print("JISZ8903: Positioning glyphs ...")

# centering
SelectWorthOutputting()
CenterInWidth()

SelectSingletons(0u3001, 0u3002, 0uff0c, 0uff0e) # "、。，．"
Move(-400, 0)
SetWidth(1300)

SelectSingletons(0u006c) # "l"
Move(30, 0)
SetWidth(650)

SelectSingletons(0u0072) # "r"
Move(35, 0)
SetWidth(650)

SelectSingletons(0u006A) # "j"
Move(-60, 0)
SetWidth(650)

SelectSingletons(0u0031) # "1"
Move(-30, 0)
SetWidth(650)

# ---- Weight

Print("JISZ8903: Tweaking weight ...")
SelectWorthOutputting()
ExpandStroke(15, 0, 0, 0, 1)

# ---- Font Generation

Print("JISZ8903: Generating ...")
SelectWorthOutputting()
SetFontNames("JISZ8903_phi", "JISZ8903 phi", "JISZ8903 phi")
SetUniqueID(0)
Generate("JISZ8903phi-Medium.ttf")

Close()
