#!/usr/bin/fontforge -script

# ---- configurations

ascii_rescale         = 95
capital_extra_rescale = 80
mw_extra_rescale      = 80
halfwidth             = 625
fullwidth             = 1250

ja_punct_kerning_offset = -400
l_kerning_offset        = 25
r_kerning_offset        = 35
j_kerning_offset        = -50
one_kerning_offset      = -25

# ---- Reencode

Open("JISZ8903-Medium.otf")
Reencode("unicode")

# ---- Rescaling

Print("JISZ8903: Rescaling some glyphs ...")

# make other ascii slightly smaller
Select(0u0020, 0u007E)          # ASCII
Scale(ascii_rescale, 100, 0, 0)

# make numbers and capital letters more smaller
Select(0u0030, 0u0039, 0u0041, 0u005A) # 0-9, A-Z
Scale(capital_extra_rescale, 100, 0, 0)

# make wide characters more smaller
SelectSingletons(0u004D, 0u0057, 0u006D, 0u0077) # M, W, m, w
Scale(mw_extra_rescale, 100, 0, 0)

# ---- Monospacify

Print("JISZ8903: Monospacifying ...")

# monospacify
Select(0u0020, 0u007E)          # ASCII
SetWidth(halfwidth)

# monospacify
SelectWorthOutputting()
SelectFewer(0u0020, 0u007E)
SetWidth(fullwidth)

# ---- Positioning

Print("JISZ8903: Positioning glyphs ...")

# centering
SelectWorthOutputting()
CenterInWidth()

SelectSingletons(0u3001, 0u3002) # "、。"
Move(ja_punct_kerning_offset, 0)
SetWidth(fullwidth)

SelectSingletons(0u006c) # "l"
Move(l_kerning_offset, 0)
SetWidth(halfwidth)

SelectSingletons(0u0072) # "r"
Move(r_kerning_offset, 0)
SetWidth(halfwidth)

SelectSingletons(0u006A) # "j"
Move(j_kerning_offset, 0)
SetWidth(halfwidth)

SelectSingletons(0u0031) # "1"
Move(one_kerning_offset, 0)
SetWidth(halfwidth)

# ---- Font Generation

Print("JISZ8903: Generating ...")
SelectWorthOutputting()
SetFontNames("JISZ8903_phi", "JISZ8903 phi", "JISZ8903 phi")
SetUniqueID(0)
Generate("JISZ8903phi-Medium.ttf")

Close()
